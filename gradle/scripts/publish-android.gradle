apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'digital.wup.android-maven-publish'

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

publishing {
    publications {
        bootsAndroid(MavenPublication) {
            groupId = boots.group
            version = boots.version
            artifactId = art()

            from components.android
            artifact sourcesJar

            pom {
                name = boots.name
                description = boots.desc
                url = boots.url

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                scm {
                    connection = boots.git
                    developerConnection = boots.git
                    url = boots.url
                }
            }
        }
    }

    repositories {
        maven {
            def release = "https://bintray.com/agoda/maven/"
            def snapshot = "https://oss.sonatype.org/content/repositories/snapshots/"

            url = boots.version.endsWith('SNAPSHOT') ? snapshot : release

            credentials {
                username System.getenv('BINTRAY_USER')
                password System.getenv('BINTRAY_KEY')
            }
        }
    }
}

signing {
    sign publishing.publications.bootsAndroid
}
